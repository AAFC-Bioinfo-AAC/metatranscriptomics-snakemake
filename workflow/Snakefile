configfile: "config/config.yaml"

from dotenv import load_dotenv
import csv, os, sys

# -------------------------------------------------------------------
# Environment and project paths
# -------------------------------------------------------------------
PIPELINE_DIR = os.getcwd()
ENV_FILE = os.path.join(PIPELINE_DIR, '.env')

# Load the .env file
load_dotenv(ENV_FILE, override=True)

## PROJECT ROOT FROM .env###
PROJECT_ROOT = os.getenv("PROJECT_ROOT")
if not PROJECT_ROOT:
    raise ValueError("PROJECT_ROOT not set in .env")

## Compose all other important paths, always relative to PROJECT_ROOT
#DATA_DIR = os.path.join(PROJECT_ROOT, "data")
#WORKSPACE_DIR = os.path.join(PROJECT_ROOT, "workspace")
CONFIG_DIR = os.path.join(PIPELINE_DIR, "config")

# -------------------------------------------------------------------
# TMPDIR setup
# -------------------------------------------------------------------

user = os.environ.get("USER")
if not user:
    raise ValueError("Environment variable USER is not set.")

fallback_tmpdir = f"/gpfs/fs7/aafc/scratch/{user}/tmpdir/metaT"
TMPDIR = os.path.expandvars(os.getenv("TMPDIR", fallback_tmpdir))
os.makedirs(TMPDIR, exist_ok=True)

# -------------------------------------------------------------------
# CARD DB setup
# -------------------------------------------------------------------

def validate_card_db(path):
    """Ensure CARD DB has required files."""
    def has_fasta(p):
        return (
            os.path.exists(os.path.join(p, "card_reference.fasta")) or
            any(name.endswith(".fasta") for name in os.listdir(p))
        )
    if not (os.path.exists(os.path.join(path, "card.json")) and has_fasta(path)):
        sys.exit(
            f"\nERROR: CARD DB not properly prepared at {path} "
            "(missing card.json or .fasta). See pipeline README.\n"
        )

env_rgi_card = os.getenv("RGI_CARD", "").strip()
if env_rgi_card:
    RGI_CARD = env_rgi_card
elif "card_latest" in config:
    cfg_card = config["card_latest"]
    RGI_CARD = os.path.join(PROJECT_ROOT, cfg_card) if not os.path.isabs(cfg_card) else cfg_card
else:
    raise ValueError("You must set RGI_CARD in .env or card_latest in config.yaml!")

if not os.path.exists(RGI_CARD):
    sys.exit(f"\nERROR: RGI_CARD path not found: {RGI_CARD}\n")

validate_card_db(RGI_CARD)

# -------------------------------------------------------------------
# Config-derived paths
# -------------------------------------------------------------------
def relpath(cfg_key):
    """Resolve a config path relative to PROJECT_ROOT."""
    return os.path.join(PROJECT_ROOT, config[cfg_key])


READS_DIR = relpath("reads_dir")
TRIMMED_DIR = os.path.join(TMPDIR, config["reads_trimmed"])
HOST_DEP_DIR = relpath("reads_host_dep")
RRNA_DEP_DIR = relpath("reads_rRNA_dep")
KRAKEN_OUTPUT_DIR = relpath("kraken_short_reads_dir")
BRACKEN_OUTPUT_DIR = relpath("bracken_short_reads_dir")
CARD_RGI_OUTPUT_DIR = relpath("amr_screening_dir")
ASSEMBLIES_DIR = relpath("assemblies_dir")
RNAQUAST_DIR = relpath("rnaquast_dir")
LOG_DIR = relpath("log_files")


BOWTIE_INDEX      = relpath("bowtie2_index")
BOWTIE_INDEX_FILES= [f"{BOWTIE_INDEX}.{i}.bt2" for i in (1,2,3,4)] + \
                    [f"{BOWTIE_INDEX}.rev.{i}.bt2" for i in (1,2)]

RRNA_DB = config["sortmerna_DB"]
TAXONOMY_DB = config["gtbd_DB"]
BUSCO_LINEAGES = config["busco_lineages"]
LINEAGES = list(BUSCO_LINEAGES.keys())

MEGAHIT_DIR = relpath("co-assembly_dir")
COASSEMBLY_INDEX = relpath("coassembly_index")
ASSEMBLY_MAPPING = relpath("sorted_bam_dir")
PRODIGAL_DIR = relpath("prodigal_dir")
FEATURECOUNTS_DIR = relpath("featurecounts_dir")

# -------------------------------------------------------------------
# Sample handling
# -------------------------------------------------------------------

SAMPLESHEET_PATH = os.path.join(CONFIG_DIR, config["samplesheet"])

def load_samples(samplesheet):
    samples = {}
    with open(samplesheet, newline="") as csvfile:
        reader = csv.DictReader(csvfile)
        for row in reader:
            samples[row["sample"]] = {
                "fastq_1": os.path.join(READS_DIR, row["fastq_1"]),
                "fastq_2": os.path.join(READS_DIR, row["fastq_2"]),
            }
    return samples

SAMPLES = load_samples(SAMPLESHEET_PATH)
SAMPLE_NAMES = list(SAMPLES.keys())

## Snakemake modules to include ##
include: "rules/preprocessing.smk"
include: "rules/sortmerna.smk"
include: "rules/taxonomy.smk"
include: "rules/amr_short_reads.smk"
include: "rules/sample_assembly.smk"
include: "rules/coassembly_annotation.smk"

# -------------------------------------------------------------------
# Rules
# -------------------------------------------------------------------

rule all:
    input:
        #Reads depleted of host, PhiX and rRNA
        expand(f"{RRNA_DEP_DIR}/{{sample}}_rRNAdep_R1.fastq.gz", sample=SAMPLES),
        expand(f"{RRNA_DEP_DIR}/{{sample}}_rRNAdep_R2.fastq.gz", sample=SAMPLES),

        #Taxonomy with Kraken
        expand(f"{KRAKEN_OUTPUT_DIR}/{{sample}}.report.txt", sample=SAMPLES),
        expand(f"{KRAKEN_OUTPUT_DIR}/{{sample}}.kraken", sample=SAMPLES),

        #Taxonomy with Bracken
        expand(f"{BRACKEN_OUTPUT_DIR}/species/{{sample}}_bracken.species.report.txt", sample=SAMPLES),
        expand(f"{BRACKEN_OUTPUT_DIR}/genus/{{sample}}_bracken.genus.report.txt", sample=SAMPLES),
        expand(f"{BRACKEN_OUTPUT_DIR}/phylum/{{sample}}_bracken.phylum.report.txt", sample=SAMPLES),

        # Merged/parsed bracken tables
        f"{BRACKEN_OUTPUT_DIR}/merged_abundance_species.txt",
        f"{BRACKEN_OUTPUT_DIR}/merged_abundance_genus.txt",
        f"{BRACKEN_OUTPUT_DIR}/merged_abundance_phylum.txt",
        f"{BRACKEN_OUTPUT_DIR}/Bracken_species_raw_abundance.csv",
        f"{BRACKEN_OUTPUT_DIR}/Bracken_species_relative_abundance.csv",
        f"{BRACKEN_OUTPUT_DIR}/Bracken_genus_raw_abundance.csv",
        f"{BRACKEN_OUTPUT_DIR}/Bracken_genus_relative_abundance.csv",
        f"{BRACKEN_OUTPUT_DIR}/Bracken_phylum_raw_abundance.csv",
        f"{BRACKEN_OUTPUT_DIR}/Bracken_phylum_relative_abundance.csv",

        # AMR screening with RGI BWT
        expand(f"{CARD_RGI_OUTPUT_DIR}/{{sample}}/{{sample}}_paired.allele_mapping_data.txt", sample=SAMPLES),

        # RNA SPAdes Assemblies and RNAQUAST evaluation with busco lineages
        expand(f"{ASSEMBLIES_DIR}/{{sample}}.fasta", sample=SAMPLES),
        expand(f"{RNAQUAST_DIR}/{{sample}}_{{lineage}}", sample=SAMPLES, lineage=LINEAGES),

        # MEGAHIT Coassembly and annotation
        f"{MEGAHIT_DIR}/final.contigs.fa",
        f"{PRODIGAL_DIR}/coassembly.faa",
        f"{PRODIGAL_DIR}/coassembly.fna",
        f"{PRODIGAL_DIR}/coassembly.gff",
        f"{PRODIGAL_DIR}/coassembly.saf",
        # Mapping of samples to coassembly, stats, and featurecounts
        expand(f"{ASSEMBLY_MAPPING}/{{sample}}.coassembly.sorted.bam", sample=SAMPLES),
        expand(f"{ASSEMBLY_MAPPING}/{{sample}}.flagstat.txt", sample=SAMPLES),
        expand(f"{ASSEMBLY_MAPPING}/{{sample}}.coverage.txt.gz", sample=SAMPLES),
        expand(f"{ASSEMBLY_MAPPING}/{{sample}}.idxstats.txt.gz", sample=SAMPLES),
        expand(f"{FEATURECOUNTS_DIR}/{{sample}}_counts.txt", sample=SAMPLES),
        
        # Required logs or done markers
        f"{LOG_DIR}/rgi_reload_db.done"